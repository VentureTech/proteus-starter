import com.proteusframework.build.*
import org.ajoberstar.gradle.git.tasks.*

import java.text.SimpleDateFormat

ext.aspectjVersion = '1.7.4'
ext.springVersion = '3.2.+'

// Compilation
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'aspectj-weave'

// Code Inspection
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

// Publishing
apply plugin: 'ivy-publish'
apply plugin: 'maven-publish'
apply plugin: 'artifactory-publish'

apply plugin: 'war'

ext.isIDEA = System.getProperties().containsKey('idea.active') || plugins.any({it.class.name == 'JetGradlePlugin'})
ext.isEclipse = System.getProperties().containsKey('eclipse.product')

buildDir = 'build/gradle'
sourceCompatibility = 1.7
version = new Version(app_version, project)
group = app_group

final repoURL = "${repo_venture_tech_url}/${repo_venture_tech_key}"
buildscript {
    final buildRepoURL = "${repo_venture_tech_url}/${repo_venture_tech_key}"
    repositories {
        maven {
            credentials {
                username "${repo_venturetech_username}"
                password "${repo_venturetech_password?:''}"
            }
            url buildRepoURL
        }
    }
    dependencies {
        // aspectj-weave plugin for our use of Spring @Configurable
        classpath 'com.i2rd:gradle-aspectj-weave:1.0',
                  'org.jfrog.buildinfo:build-info-extractor-gradle:2.2+',
                  'org.ajoberstar:gradle-git:0.6.3'
        //'org.hibernate:hibernate-gradle-plugin:4.2.8' - waiting to show up in release repos
    }
}

task populateGitInfo(type: GitBranchList) {
    // If not in the root, change the repoPath
    //repoPath = new File(project.rootProject.projectDir, "..")
}
populateGitInfo << {
    def branch = getWorkingBranch();
    branch.name;
    def commit = branch.commit
    project.ext {
        gitinfo = [
                branch: branch.name,
                ref: branch.refName,
                commit: commit.id,
                author: "${commit.author.name} <${commit.author.email}>"
        ]
    }
}
try{
    populateGitInfo.execute()
}catch(def e){
    logger.warn('Unable to retrieve GIT info. Is this in GIT?')
    project.ext {
        gitinfo = [branch: '', ref: '', commit: '', author: '']
    }
}


repositories {
    maven {
        credentials {
            username repo_venturetech_username
            password repo_venturetech_password
        }
        url repoURL
    }
}
ext.sharedManifest = manifest {
    attributes 'Implementation-Title': "${project.name.replace('-', ' ').capitalize()} Application",
        'Implementation-Version': version,
        'Commit-ID': (hasProperty('gitinfo') ? gitinfo.commit : '')
}
jar { manifest = sharedManifest }
war { manifest = sharedManifest }
configurations {
    runtimeaspects { transitive = false }
    compiletimeaspects { transitive = false }
}
// Add the APT generated folder to the java sourceSet for Intellij IDEA
sourceSets {
    main {
        java {srcDirs '.apt_generated'}
        groovy { srcDirs 'buildSrc/src/main/groovy' }
    }
    test { java {srcDirs '.apt_generated_tests'} }
}
def aptGenPath = project.file("${project.projectDir}/.apt_generated").absolutePath
def aptFilter = { p -> !p.path.startsWith(aptGenPath) }
// Remove files in .apt_generated paths as we would have dups otherwise
compileJava { source = source.filter(aptFilter) }
compileTestJava { source = source.filter(aptFilter) }


dependencies {
    runtimeaspects "org.springframework:spring-instrument:${springVersion}", "org.aspectj:aspectjweaver:${aspectjVersion}"
    compiletimeaspects "org.springframework:spring-aspects:${springVersion}", "org.aspectj:aspectjtools:${aspectjVersion}"

    // App dependencies
    compile 'net.proteusframework:proteusframework:1+'

    // Most apps use spring.
    compile "org.springframework:spring-core:${springVersion}",
        "org.springframework:spring-beans:${springVersion}",
        "org.springframework:spring-jdbc:${springVersion}",
        "org.springframework:spring-aop:${springVersion}",
        "org.springframework:spring-context:${springVersion}",
        "org.springframework:spring-orm:${springVersion}",
        "org.springframework:spring-web:${springVersion}",
        "org.springframework:spring-test:${springVersion}",
        "org.springframework:spring-instrument:${springVersion}",
        "org.springframework:spring-aspects:${springVersion}"
}

task wrapper(type: Wrapper) { gradleVersion = '1.10' }
task createRuntimeAspectsLibs(type: Copy) {
    from configurations.runtimeaspects
    into "runtime-aspects"
    rename { "${it.substring(0, it.lastIndexOf('-'))}.jar" }
}
task createCompiletimeAspectsLibs(type: Copy) {
    from configurations.compiletimeaspects
    into "compiletime-aspects"
    rename { "${it.substring(0, it.lastIndexOf('-'))}.jar" }
}
gradle.projectsEvaluated { createRuntimeAspectsLibs.execute();  createCompiletimeAspectsLibs.execute(); }



// Publishing
task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    include 'net/proteusframework/**'
}
javadoc {
    def year = new SimpleDateFormat('yyyy').format(new Date())
    def docVersion = version.originalVersion
    def isRoot = project == project.rootProject
//    def pName = project.name.capitalize()
//    include 'net/proteusframework/**'
//    options.docTitle = (isRoot ? "<b>Proteus Application Framework</b>" : "<b>${pName} API</b>")
    options.charSet = 'UTF-8'
    options.locale = 'en'
    options.noDeprecated().showFromPublic().showFromProtected()
    options.use = true
    options.version = true
    options.author = true
    options.splitIndex = true
//    options.header = ('<a href="http://www.proteusframework.com/" target="_top">Proteus Application Framework</a><br>'
//            + "${isRoot?'':pName} v${docVersion}")
//    options.windowTitle = "Proteus ${isRoot?'':pName} API v${docVersion}"
//    options.footer = """<p id="copyright" align="center";>&copy; ${year} Interactive Information R&amp;D LLC</p>"""
    options.links = [
            'http://jsr-305.googlecode.com/svn/trunk/javadoc/', 'http://docs.oracle.com/javaee/7/api/',
            'http://docs.oracle.com/javase/7/docs/api/',
            'http://docs.jboss.org/hibernate/orm/4.3/javadocs/', 'http://docs.jboss.org/hibernate/validator/5.0/api/',
            'http://docs.jboss.org/hibernate/annotations/3.5/api/', 'http://docs.jboss.org/hibernate/core/4.3/javadocs/',
            'http://docs.spring.io/spring/docs/3.2.4.RELEASE/javadoc-api/',
            'http://docs.guava-libraries.googlecode.com/git-history/release/javadoc/'
    ]
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    include 'net/proteusframework/**'
}
publishing {
    publications {
        ivyPub(IvyPublication) {
            from components.java

            artifact(sourceJar) {
                type "source"
                conf "runtime"
            }
            artifact(javadocJar) {
                type "javadoc"
                conf "runtime"
            }
        }
        mavenPub(MavenPublication) {
            from components.java

            artifact(sourceJar) {
                classifier "source"
            }
            artifact(javadocJar) {
                classifier "javadoc"
            }
        }
    }
}
artifactoryPublish.doFirst {
    if("${publish_venturetech_username}".trim().isEmpty()) println "Missing property: publish_venturetech_username"
    if("${publish_venturetech_password}".trim().isEmpty()) println "Missing property: publish_venturetech_password"
}
artifactory {
    contextUrl = publish_venture_tech_url

    publish {
        repository {
            repoKey = (version.isSnapshot() ? publish_venture_tech_snapshot_key : publish_venture_tech_release_key)
            username = publish_venturetech_username
            password = publish_venturetech_password
        }
        defaults {
            publications ('ivyPub', 'mavenPub')
            properties = ['build.status': version.status ]
        }
    }

    resolve {
        repoKey = 'libs-release'
        username = repo_venturetech_username
        password = repo_venturetech_password
    }

    clientConfig.info.vcsRevision = gitinfo.commit

}


// We will selectively enable code analysis
[Checkstyle, FindBugs].each {
    tasks.withType(it) {
        ignoreFailures = true
        enabled = false
    }
}

findbugsMain {
    enabled = true
    excludeFilter = new File("${rootProject.projectDir}/config/findbugs/exclude.xml")
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

checkstyleMain {
    enabled = true
    configFile = new File("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    classpath += configurations.compile // To avoid class loading errors when checking javadoc
    source = 'src/main/java' // Do not include generated sources
}
